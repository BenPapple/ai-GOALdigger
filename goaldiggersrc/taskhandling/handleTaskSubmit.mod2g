use "../goaldiggerProlog" as knowledge.
use "../goaldiggerAction" as actionspec.
use "../pathfinding/executeRandomMove" as module.
use "../actionhandling/preActionCleanup" as module.
use "../blockhandling/rotateBlockToSouth" as module.
use "../blockhandling/connectBlocks" as module.

/**
 * Rotate logic clearing block/obstacles at rotate target and then rotates prefering south, if block south submit action
 *
 * @author Ben G.
 */

module handleTaskSubmit {

    % submit 1-task if block is south of agent
    if bel(haveBlockAttached(true, DirBlock), DirBlock == s),
       bel(currentChosenTask(TaskFit, _, _, _, _, _, submittingAgentOneTask, _)) 
        then preActionCleanup + submit(TaskFit).

	% submit 2-task if block is south of agent
	if bel(currentChosenTask(TaskFit, TaskStep, _, _, _, _, SubmitType, _)),
	   bel(SubmitType == submittingAgentTwoTask),
	   bel(step(SimStep), TaskStep >= SimStep), 
	   bel(haveBlockAttached(true, DirBlock), DirBlock == s) 
	   	then {		
			% submit after succesful connect
			if percept(lastActionResult(success), lastAction(connect)) 
				then preActionCleanup + submit(TaskFit).
			% try to connect blocks
			if true then connectBlocks.
    } 
    
    % submit 3-task if block is south of agent
	if bel(currentChosenTask(TaskFit, TaskStep, _, _, _, _, SubmitType, _)),
	   bel(SubmitType == submittingAgentThreeTask),
	   bel(step(SimStep), TaskStep >= SimStep), 
	   bel(haveBlockAttached(true, DirBlock), DirBlock == s) 
	   	then {		
			% submit after succesful connect
			if bel(multiTaskSupporterStatus(_, _, _, _, _, _, connected, _, _, _, _, 
			       _, connected, _, _, _, _, _, _))
				then preActionCleanup + submit(TaskFit).
			% try to connect blocks
			if true then connectBlocks.
    }
    
    % submit 4-task if block is south of agent
	if bel(currentChosenTask(TaskFit, TaskStep, _, _, _, _, SubmitType, _)),
	   bel(SubmitType == submittingAgentFourTask),
	   bel(step(SimStep), TaskStep >= SimStep), 
	   bel(haveBlockAttached(true, DirBlock), DirBlock == s) 
	   	then {		
			% submit after succesful connect
			if bel(multiTaskSupporterStatus(_, _, _, _, _, _, connected, _, _, _, _, 
			       _, connected, _, _, _, _, _, connected))
				then preActionCleanup + submit(TaskFit).
			% try to connect blocks
			if true then connectBlocks.
    }

	% prefer rotate to south position and clear route to it if necessary
    if true then rotateBlockToSouth.
	
	% obligatory skip/executeRandomMove
	if true then skip.  
	
} % module