use goaldiggerProlog as knowledge.
use goaldiggerAction as actionspec.
use "./eventsubmodules/updateDispenserData" as module.
use "./eventsubmodules/updateGoalzoneData" as module.
use "./eventsubmodules/logMiniPercept" as module.
use "./eventsubmodules/handleLastActionResult" as module.
use "./eventsubmodules/handleRelativeCoordsOfAgents" as module.

/**
 * Gets executed everytime a percept or message is received and handles them
 *
 */

module goaldiggerEvent {

	/**
	 * SUBMODULE DEBUG WITH OUR MINI PERCEPT
	 *
	 */
	if bel(lDebugOn) then logMiniPercept.
	
	/**
	 * SUBMODULE MANAGING SUCCESS AND PARTIAL_SUCCESS PERCEPTS
	 *
	 */
	if true then handleLastActionResult.
	
	/**
	 * SUBMODULE MANAGING MESSAGES !!! PLACE AFTER handleLastActionResult MOVE !!!
	 *
	 */
	if true then handleRelativeCoordsOfAgents.

	
	/**
	 * SUBMODULE UPDATE GOALZONE MAP
	 *
	 */
	if true then updateGoalzoneData.


	/**
	 * SUBMODULE UPDATE DISPENSER MAP
	 *
	 */
	if true then updateDispenserData.

	/**
	 * SWITCH MAIPULATION
	 *
	 */	

	% check old dispenser request and set switch to false
	if percept(step(Step)), bel(haveDispenserDelivery(true, OnStepX), OnStepX + 1 =< Step) then delete(haveDispenserDelivery(true, OnStepX)) + insert(haveDispenserDelivery(false, Step)).
	
	% check attached block direction for block and if none found switch haveBlockAttached false
	if bel(haveBlockAttached(true, DirBlock), directionToCoordinate(DirBlock, X, Y)), not(percept(attached(X, Y))) then 
		delete(haveBlockAttached(true, DirBlock)) + insert(haveBlockAttached(false, DirBlock)) + delete(executeManhattan).

	% when at manhattan target deactivate manhattan switch, XY same Coord or one value max 1 different 
	if bel(haveBlockAttached(false,_), targetMd(X, Y), agentAt(A, B), abs(A)-abs(X) =< 1, abs(B)-abs(Y) =< 1, executeManhattan) then delete(executeManhattan).
	
	% deactivate manhattan switch if in goalzone with block
	if bel(haveBlockAttached(true,_), targetMd(X, Y), agentAt(A, B), A == X, B == Y, executeManhattan), percept(goalZone(0, 0)) then delete(executeManhattan).
	
	
	/**
	 * MISCELLANEOUS POST EVERYTHING
	 *
	 */	
	 
	% change affinity direction after X steps
	if bel(changeAffinityAfterTheseSteps(ChangeStep)), percept(step(X), X >= 2, 0 is mod(X, ChangeStep)), bel(randomAffinity(DirOld), randomGoForwardDirection(DirOld, NewDir)) then 
		delete (randomAffinity(DirOld)) + insert (randomAffinity(NewDir)).
	
	% when role default store first seen RoleZone coordinates as targetMd
	if percept(roleZone(X, Y), role(default)), bel(agentAt(X2, Y2), targetMd(X4, Y4), localize(X, Y, X2, Y2, X3, Y3)), not(bel(executeManhattan)) then 
		delete(targetMd(X4, Y4)) + insert(targetMd(X3, Y3)) + insert(executeManhattan).

		
	% when role worker with block put first seen goalzone coordinates as targetMd
	if percept(goalZone(X, Y), role(worker)), bel(haveBlockAttached(true,_), agentAt(X2, Y2), targetMd(X4, Y4), localize(X, Y, X2, Y2, X3, Y3)), not(bel(executeManhattan)) then 
		delete(targetMd(X4, Y4)) + insert(targetMd(X3, Y3)) + insert(executeManhattan).
	
	% when role worker with block put from percept seen goalzone coordinates as targetMd because of disappearing goalzones
	if percept(goalZone(X, Y), role(worker)), bel(executeManhattan, haveBlockAttached(true,_), agentAt(X2, Y2), targetMd(X4, Y4), localize(X, Y, X2, Y2, X3, Y3)) then 
		delete(targetMd(X4, Y4)) + insert(targetMd(X3, Y3)).
	
	% when role worker on goalzone without block deactivate manhattan switch
	if percept(goalZone(0, 0), role(worker)), bel(haveBlockAttached(false,_)), bel(executeManhattan) then delete(executeManhattan).
	
	% when role worker with block because of disappearing goalzones has to check if goal around manhattan target
	if percept(haveBlockAttached(true,_), role(worker)), not(percept(goalZone(_, _))), bel(executeManhattan, agentAt(X2, Y2), targetMd(X4, Y4), abs(X2 - X4) + abs(Y2 - Y4) =< 2 ) then 
		delete(executeManhattan).
		
	% With block update goalzone target MD
	if bel(haveBlockAttached(true, _), targetMd(X, Y), targetClosestGoalZone(A, B, _)) then delete(targetMd(X, Y)) + insert(targetMd(A, B)) 
		+ insert(executeManhattan).

	/**
	 * ACTIVATE MAIN LOOP
	 *
	 */	

	% Update step counter with new step when new percept step available
	if percept(step(X)), bel(step(Y), X\==Y, X>0) then {
		if true then delete(step(Y)) + insert(step(X)).
		if bel(haveMove) then delete(haveMove).
	}
	
} % module