use "../goaldiggerProlog" as knowledge.
use "../goaldiggerAction" as actionspec.
use "../pathfinding/executeRandomMove" as module.
use "../actionhandling/preActionCleanup" as module.

/**
 * Rotate logic clearing block/obstacles at rotate target and then rotates
 *
 */

module rotateBlockAgainstAffinity {

	 % rotate random if block in affinity direction
	 if  bel(haveBlockAttached(true, DirBlock), directionToCoordinate(DirBlock, A, B)), percept(attached(A, B)), 
	 bel(randomAffinity(DirAffi), DirBlock == DirAffi, randomRotate(Rot), oppositeRotate(Rot, AltRot), rotateToCoord(DirBlock, Rot, X, Y), rotateToCoord(DirBlock, AltRot, F, G)) then {
	 
	 	 % rotate if free
		 if not(percept(thing(X, Y, Type, _), Type \== dispenser)) then rotate(Rot) +  preActionCleanup.
		 if not(percept(thing(F, G, Type, _), Type \== dispenser)) then rotate(AltRot) +  preActionCleanup.

		 % clear blocked rotate targets
		 if percept(thing(X, Y, obstacle, _)) then clear(X, Y) +  preActionCleanup.
		 if percept(thing(X, Y, block, _)) then clear(X, Y) +  preActionCleanup.
		 if percept(thing(F, G, obstacle, _)) then clear(F, G) +  preActionCleanup.
		 if percept(thing(F, G, block, _)) then clear(F, G) +  preActionCleanup.
	 	
	 } % if rotate random

	 % rotate random if block 90 degree to affinity direction
	 if  bel(haveBlockAttached(true, DirBlock), directionToCoordinate(DirBlock, A, B)), percept(attached(A, B)), 
	 bel(randomAffinity(DirAffi), oppositeDirection(DirAffi, DirOppo), DirBlock \== DirAffi, DirBlock \== DirOppo, rotateAgainstAffinity(DirAffi, DirBlock, Rot), rotateToCoord(DirBlock, Rot, X, Y)) then {
	 
		 if not(percept(thing(X, Y, Type, _), Type \== dispenser)) then rotate(Rot) +  preActionCleanup.
		 if percept(thing(X, Y, obstacle, _)) then clear(X, Y) +  preActionCleanup.
		 if percept(thing(X, Y, block, _)) then clear(X, Y) +  preActionCleanup.

	} % rotate random if block 90 degree
	
	% obligatory skip
	if true then skip +  preActionCleanup.  
	
} % module