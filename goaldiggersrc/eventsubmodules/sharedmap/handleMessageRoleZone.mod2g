use "../../goaldiggerProlog" as knowledge.
use "../../goaldiggerAction" as actionspec.

/**
 *
 * Handles messages about sighted role zones sent by other agents
 *
 * @author Isaac S.
 *
 */

module handleMessageRoleZone {
	
	%ToDo: There doesn't seem to be a flag to recalc the distance to role zones, as for goal zones. Is it so by design?
	
	% Check cached msgRoleZoneData messages.
	forall bel(cachedMsgRoleZoneData(X,Y,SenderName)) 
		do {
			if bel(confirmedOffsetOfAgent(OffsetX,OffsetY,SenderName), X1 is X + OffsetX, Y1 is Y + OffsetY) 
				then {
		    		if not(bel(mapRoleZone(X1,Y1,_))) 
		    			then insert(mapRoleZone(X1,Y1, 9999)).
		    		if bel(mapRoleZone(X1,Y1,_)) 
		    			then delete(cachedMsgRoleZoneData(X,Y,SenderName)).
		} % then
	} %forall
	
	% Check received msgRoleZoneData messages.
	forall (_).sent(msgRoleZoneData(X,Y,SenderName)) 
		do {
			if bel(confirmedOffsetOfAgent(OffsetX,OffsetY,SenderName), X1 is X + OffsetX, Y1 is Y + OffsetY),
			   not(bel(mapRoleZone(X1,Y1,_))) 
			    then insert(mapRoleZone(X1,Y1, 9999)).
			if not(bel(confirmedOffsetOfAgent(_,_,SenderName), cachedMsgRoleZoneData(X,Y,SenderName))) 
			    then insert(cachedMsgRoleZoneData(X,Y,SenderName)).
	}
	
}