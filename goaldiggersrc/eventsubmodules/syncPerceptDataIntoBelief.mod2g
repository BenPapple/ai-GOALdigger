use "../goaldiggerProlog" as knowledge.
use "../goaldiggerAction" as actionspec.

/**
 * Store (or delete if not in percept anymore) objects from percept into belief with corrected coordinates
 *
 */

module syncPerceptDataIntoBelief {

	% Update step counter with new step when new percept step available
	if percept(step(X)), bel(step(Y), X\==Y, X>0) then delete(step(Y)) + insert(step(X)).
	
	% CREATE belief about status messages
	forall (SenderConnect).sent(otherAgentStatus(SenderName, X, Y, MsgStep, Seed, Role, BlockTypeAttached)) do {
	    if not(bel(storedOtherAgentStatus(SenderName, _, _, _, _, _, _, _))) then 
	        insert(storedOtherAgentStatus(SenderName, MsgStep, Role, Seed, SenderConnect, X, Y, BlockTypeAttached)).
	}
	    
	% UPDATE belief about status messages
	forall (SenderConnect).sent(otherAgentStatus(SenderName, X, Y, MsgStep, Seed, Role, BlockTypeAttached)) do {
	    if bel(storedOtherAgentStatus(SenderName, OldMsgStep, V3, V4, V5, V6, V7, V8), MsgStep > OldMsgStep) then 
	        delete(storedOtherAgentStatus(SenderName, OldMsgStep, V3, V4, V5, V6, V7, V8)) + 
	        insert(storedOtherAgentStatus(SenderName, MsgStep, Role, Seed, SenderConnect, X, Y, BlockTypeAttached)).
	}
	
	% STORE unknown dispensers from percept with MD field
	forall percept(thing(X, Y, dispenser, Details)) do {
	    if bel(agentAt(X2, Y2), localize(X, Y, X2, Y2, X3, Y3)), 
	    not(bel((storedDispenser(X3, Y3, dispenser, Details, _, _)))) then 
	        insert(storedDispenser(X3, Y3, dispenser, Details, 0, false)).
	}	
	
	% STORE unknown goalzones with MD field
	forall percept(goalZone(X, Y)) do {
	    if bel(agentAt(X2, Y2), localize(X, Y, X2, Y2, X3, Y3)), not(bel((storedGoalZone(X3, Y3, _)))) then 
		    insert(storedGoalZone(X3, Y3, 0)).
    }

	% DELETE dispenser from belief if not seen in percept		
	forall bel(storedDispenser(X1, Y1, dispenser, Details, MDValue, Bool)) do {
	    if bel( agentAt(X2, Y2), X3 is X1 - X2, Y3 is Y1 - Y2, abs(X3) + abs(Y3) =< 5),
	       bel(storedDispenser(X1, Y1, dispenser, Details, MDValue, Bool)),
	       not(percept(thing(X3, Y3, dispenser, _))) 
		    then delete(storedDispenser(X1, Y1, dispenser, Details, MDValue, Bool)).
    }
		
	% DELETE disappeared goalzones
	forall bel(storedGoalZone(X, Y, Md)) do{
	    if bel(agentAt(X2, Y2), X3 is X - X2, Y3 is Y - Y2, abs(X3) + abs(Y3) =< 5),
	       bel(storedGoalZone(X, Y, Md)),
	       not(percept((goalZone(X3, Y3)))) then delete(storedGoalZone(X, Y, Md)).
	}
	
	% ToDo too complex agent only needs to store it once not delayed or cached, just overwrite old value with new
    % ToDo sent(otherAgentAt replaced with storedOtherAgentStatus... in belief
	% The agent processes received messages about other agents' positions if it already knows it offsets to those agents
%	forall (_).sent(otherAgentAt(Name, X0, Y0, Step)), 
%	       bel(agentOffset(Name, OffsetToEntityX, OffsetToEntityY, _), step(Step), X1 is X0 + OffsetToEntityX, 
%	           Y1 is Y0 + OffsetToEntityY, worldSizeX(SizeX), worldSizeY(SizeY), 
%	           getModPos(X1, Y1, SizeX, SizeY, X2, Y2)) 
%		do insert(otherAgentAt(Name, X2, Y2, Step)).
%
%	% Remove old otherAgentAt variables:
%	forall percept(step(CurrentStep)), bel(messageProcessingDelay(Delay), messagePersitanceAfterDelay(Persistance), 
%	                                       StepLimit is CurrentStep - Delay - Persistance, 
%		                                   otherAgentAt(AgentNameSender, X, Y, Step), Step < StepLimit) 
%		do delete(otherAgentAt(AgentNameSender,X,Y,Step)). 
	
} % end module