use "../goaldiggerProlog" as knowledge.
use "../goaldiggerAction" as actionspec.

/**
 * Store (or delete if not in percept anymore) objects from percept into belief with corrected coordinates
 *
 */

module syncPerceptDataIntoBelief {

	% Update step counter with new step when new percept step available
	if percept(step(X)), bel(step(Y), X\==Y, X>0) then delete(step(Y)) + insert(step(X)).
	
	% DELETE out of step storedOtherAgentStatus
	forall percept(step(SimStep)),
	       bel(storedOtherAgentStatus(SenderName, MsgStep, Role, Seed, SenderConnect, X, Y, BlockTypeAttached),
	           MsgStep > SimStep) 
	    do {
	    if bel(storedOtherAgentStatus(SenderName, MsgStep, Role, Seed, SenderConnect, X, Y, BlockTypeAttached))
	        then delete(storedOtherAgentStatus(SenderName, MsgStep, Role, Seed, SenderConnect, X, Y, BlockTypeAttached)).
	}
	
	% CREATE belief about status messages
	forall (SenderConnect).sent(otherAgentStatus(SenderName, X, Y, MsgStep, Seed, Role, BlockTypeAttached)) do {
	    if not(bel(storedOtherAgentStatus(SenderName, _, _, _, _, _, _, _))) then 
	        insert(storedOtherAgentStatus(SenderName, MsgStep, Role, Seed, SenderConnect, X, Y, BlockTypeAttached)).
	}
	    
	% UPDATE belief about status messages
	forall (SenderConnect).sent(otherAgentStatus(SenderName, X, Y, MsgStep, Seed, Role, BlockTypeAttached)) do {
	    if bel(storedOtherAgentStatus(SenderName, OldMsgStep, V3, V4, V5, V6, V7, V8), MsgStep > OldMsgStep) then 
	        delete(storedOtherAgentStatus(SenderName, OldMsgStep, V3, V4, V5, V6, V7, V8)) + 
	        insert(storedOtherAgentStatus(SenderName, MsgStep, Role, Seed, SenderConnect, X, Y, BlockTypeAttached)).
	}
	
	% STORE unknown dispensers from percept with MD field
	forall percept(thing(X, Y, dispenser, Details), name(MyName)) do {
	    if bel(agentAt(X2, Y2, _), localize(X, Y, X2, Y2, X3, Y3)), 
	    not(bel((storedDispenser(X3, Y3, dispenser, Details, _, _)))) then {
	        if true then insert(storedDispenser(X3, Y3, dispenser, Details, 0, false))
	        + allother.send(msgDispenserData(X3, Y3, dispenser, Details, false, MyName)).	        
	    }
	}	
	
	% STORE unknown goalzones with MD field
	forall percept(goalZone(X, Y)) do {
	    if bel(agentAt(X2, Y2, _), localize(X, Y, X2, Y2, X3, Y3)), not(bel((storedGoalZone(X3, Y3, _)))) then {
		    if true then insert(storedGoalZone(X3, Y3, 0)).
		    if not(bel(calculateNewGoalzoneMD)) then insert(calculateNewGoalzoneMD).
		}
    }
    
    % STORE unknown rolezones with MD field
	forall percept(roleZone(X, Y)) do {
	    if bel(agentAt(X2, Y2, _), localize(X, Y, X2, Y2, X3, Y3)), not(bel((storedRoleZone(X3, Y3, _)))) then 
		    insert(storedRoleZone(X3, Y3, 0)).
    }

	% DELETE dispenser from belief if not seen in percept		
	forall bel(storedDispenser(X1, Y1, dispenser, Details, MDValue, Bool)) do {
	    if bel(storedDispenser(X1, Y1, dispenser, Details, MDValue, Bool)),
	       bel( agentAt(X2, Y2, _), X3 is X1 - X2, Y3 is Y1 - Y2, abs(X3) + abs(Y3) =< 5),
	       not(percept(thing(X3, Y3, dispenser, _))) 
		    then delete(storedDispenser(X1, Y1, dispenser, Details, MDValue, Bool)).
    }
		
	% DELETE disappeared goalzones
	forall bel(storedGoalZone(X, Y, Md)) do{
	    if bel(storedGoalZone(X, Y, Md)), bel(agentAt(X2, Y2, _), X3 is X - X2, Y3 is Y - Y2, abs(X3) + abs(Y3) =< 5),
	       not(percept((goalZone(X3, Y3)))) then delete(storedGoalZone(X, Y, Md)).
	       % TO DO: In order to activate the messages for deletd goal zones, add:
	       % To the conditions: percept(name(MyName))
	       % To the actions: allother.send(messageDeletedGoalZone(X, Y, MyName))	       
	}
	
	% DELETE disappeared rolezones
	forall bel(storedRoleZone(X, Y, Md)) do{
	    if bel(storedRoleZone(X, Y, Md)), bel(agentAt(X2, Y2, _), X3 is X - X2, Y3 is Y - Y2, abs(X3) + abs(Y3) =< 5),
	       not(percept((roleZone(X3, Y3)))) then delete(storedRoleZone(X, Y, Md)).
  	}
	
	% ToDo too complex agent only needs to store it once not delayed or cached, just overwrite old value with new
    % ToDo sent(otherAgentAt replaced with storedOtherAgentStatus... in belief
	% The agent processes received messages about other agents' positions if it already knows it offsets to those agents
%	forall (_).sent(otherAgentAt(Name, X0, Y0, Step)), 
%	       bel(agentOffset(Name, OffsetToEntityX, OffsetToEntityY, _), step(Step), X1 is X0 + OffsetToEntityX, 
%	           Y1 is Y0 + OffsetToEntityY, worldSizeX(SizeX), worldSizeY(SizeY), 
%	           getModPos(X1, Y1, SizeX, SizeY, X2, Y2)) 
%		do insert(otherAgentAt(Name, X2, Y2, Step)).
%
%	% Remove old otherAgentAt variables:
%	forall percept(step(CurrentStep)), bel(messageProcessingDelay(Delay), messagePersitanceAfterDelay(Persistance), 
%	                                       StepLimit is CurrentStep - Delay - Persistance, 
%		                                   otherAgentAt(AgentNameSender, X, Y, Step), Step < StepLimit) 
%		do delete(otherAgentAt(AgentNameSender,X,Y,Step)). 
	
} % end module