use "../goaldiggerProlog" as knowledge.
use "../goaldiggerAction" as actionspec.
use "executeRandomMove" as module.
use "../actionhandling/postActionCleanup" as module.

/**
 * Handling movement, attaching and requesting when agent around dispenser
 *
 */

module stepAndRequestAroundDispenser {	

	% go random explore when current task expired
	if percept(step(SimStep), role(worker)), bel(haveBlockAttached(false, _), currentChosenTask(_, TaskStep, _, _, _, _,_,_), TaskStep < SimStep) then executeRandomMove.

	% handle situation when agent on top of dispenser
	if bel(haveBlockAttached(false, _), currentChosenTask(_, _, _, _, _, BlockType,_,_)), percept(thing(0, 0, dispenser, BlockType), role(worker)) 
		then executeRandomMove.


	% Block attaching if in position
	if  bel(haveBlockAttached(false,_)), bel(haveDispenserDelivery(true, X), step(Step), X  =< Step + 1, currentChosenTask(_, _, _, _, _, BlockType,_,_)),
	 percept(thing(A, B, block, BlockType), abs(A) =< 1, abs(B) =< 1) then {
	
		if percept(thing(1, 0, block,_), thing(1, 0, dispenser,_)), not(percept(attached(1, 0))) then postActionCleanup + attach(e).
		if percept(thing(-1, 0, block,_), thing(-1, 0, dispenser,_)), not(percept(attached(-1, 0))) then postActionCleanup + attach(w).
		if percept(thing(0, -1, block,_), thing(0, -1, dispenser,_)), not(percept(attached(0, -1))) then postActionCleanup + attach(n).
		if percept(thing(0, 1, block,_), thing(0, 1, dispenser,_)), not(percept(attached(0, 1))) then postActionCleanup + attach(s).
	
	}

	% request block if in position nswe of dispenser
	if  bel(haveBlockAttached(false,_)), bel(currentChosenTask(_, _, _, _, _, BlockType,_,_)), 
		percept(thing(A, B, dispenser, BlockType), abs(A) =< 1, abs(B) =< 1) then {
	
		% request block when nswe of dispenser
		if percept(thing(1, 0, dispenser,_)), not(percept(thing(1, 0, block,_))) then postActionCleanup + request(e).
		if percept(thing(-1, 0, dispenser,_)), not(percept(thing(-1, 0, block,_))) then postActionCleanup + request(w).
		if percept(thing(0, 1, dispenser,_)), not(percept(thing(0, 1, block,_))) then postActionCleanup + request(s).
		if percept(thing(0, -1, dispenser,_)), not(percept(thing(0, -1, block,_))) then postActionCleanup + request(n).
		
		% clear block when nswe of dispenser
		if percept(thing(1, 0, dispenser,_)), percept(thing(1, 0, block,_)), not(percept(attached(1, 0))) then postActionCleanup + clear(1, 0).
		if percept(thing(-1, 0, dispenser,_)), percept(thing(-1, 0, block,_)), not(percept(attached(-1, 0))) then postActionCleanup + clear(-1, 0).
		if percept(thing(0, 1, dispenser,_)), percept(thing(0, 1, block,_)), not(percept(attached(0, 1))) then postActionCleanup + clear(0, 1).
		if percept(thing(0, -1, dispenser,_)), percept(thing(0, -1, block,_)), not(percept(attached(0, -1))) then postActionCleanup + clear(0, -1).
	
	}

	% if entity around and block on near dispenser move out and skip X turns
	if  bel(haveBlockAttached(false,_), currentChosenTask(_, _, _, _, _, BlockType,_,_)),  
		percept(thing(A, B, dispenser, BlockType), abs(A) =< 1, abs(B) =< 1) then {
	
		if percept(thing(X, Y, entity, _), abs(X) =< 1, abs(Y) =< 1), bel(skipThisStep(SkipStep), step(Step), skipRandomSteps(RandSteps)) then { 
			if percept(thing(1, 0, dispenser,_)) then 
			postActionCleanup + move(w) + delete(skipThisStep(SkipStep)) + insert(skipThisStep(Step + RandSteps)).
			if percept(thing(-1, 0, dispenser,_)) then 
			postActionCleanup + move(e) + delete(skipThisStep(SkipStep)) + insert(skipThisStep(Step + RandSteps)).
			if percept(thing(0, 1, dispenser,_)) then 
			postActionCleanup + move(n) + delete(skipThisStep(SkipStep)) + insert(skipThisStep(Step + RandSteps)).
			if percept(thing(0, -1, dispenser,_)) then 
			postActionCleanup + move(s) + delete(skipThisStep(SkipStep)) + insert(skipThisStep(Step + RandSteps)).
		}
		
	} % other entity around
	
	% Moving into position nswe of dispenser 
	if  bel(haveBlockAttached(false,_)), bel(currentChosenTask(_, _, _, _, _, BlockType,_,_)), 
		percept(thing(A, B, dispenser, BlockType), abs(A) =< 1, abs(B) =< 1), not(percept(thing(A, B, block, _))) then {
		
		% Dispenser is diagonal relative to agent, agents steps nswe of dispenser or prepares clear of movement target
		if bel(A = 1, B = -1), bel(directionToCoordinate(e, C, D)), not(percept(thing(C, D, Type, _), Type \== dispenser)) then postActionCleanup + move(e).
		if bel(A = 1, B = -1), bel(directionToCoordinate(s, X, Y)), not(percept(thing(X, Y, Type, _), Type \== dispenser)) then postActionCleanup + move(n).		
		if bel(directionToCoordinate(e, C, D)), percept(thing(C, D, block, _)) then  postActionCleanup + clear(C, D).
		if bel(directionToCoordinate(e, C, D)), percept(thing(C, D, obstacle, _)) then  postActionCleanup + clear(C, D).		

		if bel(A = -1, B = 1), bel(directionToCoordinate(n, C, D)), not(percept(thing(C, D, Type, _), Type \== dispenser)) then postActionCleanup + move(s).
		if bel(A = -1, B = 1), bel(directionToCoordinate(w, X, Y)), not(percept(thing(X, Y, Type, _), Type \== dispenser)) then postActionCleanup + move(w).
		if bel(directionToCoordinate(n, C, D)), percept(thing(C, D, obstacle, _)) then postActionCleanup + clear(C, D).
		if bel(directionToCoordinate(n, C, D)), percept(thing(C, D, block, _)), not(percept(attached(C, D))) then postActionCleanup + clear(C, D).
		
		if bel(A = -1, B = -1), bel(directionToCoordinate(w, C, D)), not(percept(thing(C, D, Type, _), Type \== dispenser)) then postActionCleanup + move(w).
		if bel(A = -1, B = -1), bel(directionToCoordinate(s, X, Y)), not(percept(thing(X, Y, Type, _), Type \== dispenser)) then postActionCleanup + move(n).
		if bel(directionToCoordinate(w, C, D)), percept(thing(C, D, block, _)), not(percept(attached(C, D))) then postActionCleanup + clear(C, D).
		if bel(directionToCoordinate(w, C, D)), percept( thing(C, D, obstacle, _)) then postActionCleanup + clear(C, D).
		
		if bel(A = 1, B = 1), bel(directionToCoordinate(e, C, D)), not(percept(thing(C, D, Type, _), Type \== dispenser)) then postActionCleanup + move(e).
		if bel(A = 1, B = 1), bel(directionToCoordinate(n, X, Y)), not(percept(thing(X, Y, Type, _), Type \== dispenser)) then postActionCleanup + move(s).
		if bel(directionToCoordinate(e, C, D)), percept(thing(C, D, obstacle, _)) then postActionCleanup + clear(C, D).
		if bel(directionToCoordinate(e, C, D)), percept(thing(C, D, block, _)), not(percept(attached(C, D))) then postActionCleanup + clear(C, D).		
		
	} % if
	
	% obligatory skip.
	if true then postActionCleanup + skip.
	
} % module